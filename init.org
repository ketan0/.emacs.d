#+TITLE: <~~emacs config~~>
#+AUTHOR: Ketan Agrawal
#+BABEL: :cache yes
#+LATEX_HEADER: \usepackage{parskip}
#+LATEX_HEADER: \usepackage{inconsolata}
#+LATEX_HEADER: \usepackage[utf8]{inputenc}
#+PROPERTY: header-args :tangle yes
^above line tangles code blocks by default

Inspiration for this org-based setup comes from [[https://github.com/larstvei/dot-emacs][larstvei]] and [[https://github.com/danielmai/.emacs.d/blob/master/config.org][danielmai]].

* Setup
** Package manager bootstrap
   #+BEGIN_SRC emacs-lisp
     ;; BOOTSTRAP STRAIGHT.EL
     (defvar bootstrap-version)
     (let ((bootstrap-file
            (expand-file-name "straight/repos/straight.el/bootstrap.el" user-emacs-directory))
           (bootstrap-version 5))
       (unless (file-exists-p bootstrap-file)
         (with-current-buffer
             (url-retrieve-synchronously
              "https://raw.githubusercontent.com/raxod502/straight.el/develop/install.el"
              'silent 'inhibit-cookies)
           (goto-char (point-max))
           (eval-print-last-sexp)))
       (load bootstrap-file nil 'nomessage))
     (straight-use-package 'use-package)
     (setq straight-use-package-by-default t)


   #+END_SRC
** Tangle source code
 #+BEGIN_SRC emacs-lisp
   (defun tangle-init ()
     "If the current buffer is 'init.org' the code-blocks are
   tangled, and the tangled file is compiled."
     (when (equal (buffer-file-name)
                  (expand-file-name (concat user-emacs-directory "init.org")))
       ;; Avoid running hooks when tangling.
       (let ((prog-mode-hook nil))
         (org-babel-tangle)
         (byte-compile-file (concat user-emacs-directory "init.el")))))


   (add-hook 'after-save-hook 'tangle-init)

 #+END_SRC
** Move frame to right half of screen
   Right-aligns my emacs window. (Why isn't this working on startup...)
#+BEGIN_SRC emacs-lisp
  (defun get-string-from-file (filePath)
    "Return filePath's file content."
    (with-temp-buffer
      (insert-file-contents filePath)
      (buffer-string)))

  (add-hook 'after-make-frame-functions
            (lambda (frame)
              (if (string-equal system-type "darwin")
                  (do-applescript
                   (get-string-from-file "~/Documents/rightalign.txt")))))
#+END_SRC
** Variable customizations
   See [[file:custom.el][custom.el]] for variables edited via customization buffer.
   #+BEGIN_SRC emacs-lisp
     (setq custom-safe-themes t)
     (setq custom-file (concat user-emacs-directory "custom.el"))
     (load custom-file)
   #+END_SRC
* General Convenience
** TODO Clean this up, also there's some stuff near evil-leader too
   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "A-<backspace>") 'backward-kill-word)
     (global-set-key (kbd "M-m") 'suspend-frame)
     (global-set-key (kbd "M-q") 'save-buffers-kill-emacs)
     (global-set-key (kbd "C-M-f") 'toggle-frame-fullscreen)

     (defun er-find-user-init-file ()
       "Edit the `user-init-file', in *this* window."
       (interactive)
       (find-file (concat user-emacs-directory "init.org")))

     (defun open-dir-in-finder ()
       "Open a new Finder window to the path of the current buffer"
       (interactive)
       (start-process "mai-open-dir-process" nil "open" "."))

     (defun open-dir-in-iterm ()
       "Open the current directory of the buffer in iTerm."
       (interactive)
       (let* ((iterm-app-path "/Applications/iTerm.app")
              (iterm-brew-path "/opt/homebrew-cask/Caskroom/iterm2/1.0.0/iTerm.app")
              (iterm-path (if (file-directory-p iterm-app-path)
                              iterm-app-path
                            iterm-brew-path)))
         (start-process "mai-open-dir-process" nil "open" "-a" iterm-path ".")))

     (defun xah-new-empty-buffer ()
       "Create a new empty buffer.
          New buffer will be named “untitled” or “untitled<2>”, “untitled<3>”, etc.

          It returns the buffer (for elisp programing).

          URL `http://ergoemacs.org/emacs/emacs_new_empty_buffer.html'
          Version 2017-11-01"
       (interactive)
       (let (($buf (generate-new-buffer "untitled")))
         (switch-to-buffer-other-window $buf)
         (funcall initial-major-mode)
         (setq buffer-offer-save t)
         $buf))

   #+END_SRC 
* Appearances
  Fira Code is a pleasing font.
  #+BEGIN_SRC emacs-lisp
    (set-frame-font "Fira Code 14" nil t)
    ;;Fira Code ligatures
    (if (string-equal system-type "darwin")
        (mac-auto-operator-composition-mode t))
    
  #+END_SRC
  My current color theme and powerline theme.
  #+BEGIN_SRC emacs-lisp
    (defun switch-theme (theme)
      "Disables any currently active themes and loads THEME."
      ;; This interactive call is taken from `load-theme'
      (interactive
       (list
        (intern (completing-read "Load custom theme: "
                                 (mapc 'symbol-name
                                       (custom-available-themes))))))
      (let ((enabled-themes custom-enabled-themes))
        (mapc #'disable-theme custom-enabled-themes)
        (load-theme theme t)
        (load-theme 'airline-distinguished t)))

    (defun disable-active-themes ()
      "Disables any currently active themes listed in `custom-enabled-themes'."
      (interactive)
      (mapc #'disable-theme custom-enabled-themes))

    (use-package spacemacs-theme
      :defer t)

    (use-package leuven-theme
      :defer t)

    (use-package constant-theme
      :defer t)

    (use-package cherry-blossom-theme
      :defer t)

    (use-package gruvbox-theme
      :defer t)

    (use-package dracula-theme
      :defer t)

    (use-package bubbleberry-theme
      :defer t)

    (use-package airline-themes
      :config (load-theme 'airline-distinguished))


  #+END_SRC
* Defaults
  Some sane defaults, mostly taken from [[https://github.com/danielmai/.emacs.d/blob/master/config.org][Daniel Mai]]
  
  #+BEGIN_SRC emacs-lisp 
    (setq inhibit-splash-screen t) ;don't show default emacs startup screen
    (setq visible-bell t) ;Instead of shell bell, visual flash
    (electric-pair-mode t) ;;auto-pairs, eg () [] {}
    (when window-system
      (menu-bar-mode -1)
      (tool-bar-mode -1)
      (scroll-bar-mode -1)
      (tooltip-mode -1))
    (global-visual-line-mode t)

    ;; These functions are useful. Activate them.
    (put 'downcase-region 'disabled nil)
    (put 'upcase-region 'disabled nil)
    (put 'narrow-to-region 'disabled nil)
    (put 'dired-find-alternate-file 'disabled nil)

    ;; Answering just 'y' or 'n' will do
    (defalias 'yes-or-no-p 'y-or-n-p)

    ;; Keep all backup and auto-save files in one directory
    (setq backup-directory-alist '(("." . "~/.emacs.d/backups")))
    (setq auto-save-file-name-transforms '((".*" "~/.emacs.d/auto-save-list/" t)))

    ;; UTF-8 please
    (setq locale-coding-system 'utf-8) ; pretty
    (set-terminal-coding-system 'utf-8) ; pretty
    (set-keyboard-coding-system 'utf-8) ; pretty
    (set-selection-coding-system 'utf-8) ; please
    (prefer-coding-system 'utf-8) ; with sugar on top

    ;; Turn on the blinking cursor
    (blink-cursor-mode t)

    (setq-default indent-tabs-mode nil)
    (setq-default indicate-empty-lines t)

    ;; Don't count two spaces after a period as the end of a sentence.
    ;; Just one space is needed.
    (setq sentence-end-double-space nil)

    (show-paren-mode t)
    (column-number-mode t)

    (setq uniquify-buffer-name-style 'forward)

    ;; -i gets alias definitions from .bash_profile
    (setq shell-command-switch "-ic")

    (when (version<= "26.0.50" emacs-version)
      (global-display-line-numbers-mode))
  #+END_SRC
* Packages
** Org
*** org
     #+BEGIN_SRC emacs-lisp
       ;;______________________________________________________________________
       ;;;;  Installing Org with straight.el
       ;;; https://github.com/raxod502/straight.el/blob/develop/README.md#installing-org-with-straightel
       (require 'subr-x)
       (use-package git)

       (defun org-git-version ()
         "The Git version of 'org-mode'.
       Inserted by installing 'org-mode' or when a release is made."
         (require 'git)
         (let ((git-repo (expand-file-name
                          "straight/repos/org/" user-emacs-directory)))
           (string-trim
            (git-run "describe"
                     "--match=release\*"
                     "--abbrev=6"
                     "HEAD"))))

       (defun org-release ()
         "The release version of 'org-mode'.
       Inserted by installing 'org-mode' or when a release is made."
         (require 'git)
         (let ((git-repo (expand-file-name
                          "straight/repos/org/" user-emacs-directory)))
           (string-trim
            (string-remove-prefix
             "release_"
             (git-run "describe"
                      "--match=release\*"
                      "--abbrev=0"
                      "HEAD")))))

       (provide 'org-version)

       ;; (straight-use-package 'org) ; or org-plus-contrib if desired

       (use-package org
         :config
         (setq org-log-done t)
         (setq org-directory "~/org")

         (setq org-mobile-inbox-for-pull "~/org/flagged.org")

         (setq org-mobile-directory "~/Dropbox/Apps/Organ/")

         (setq org-src-tab-acts-natively t)
         (setq org-agenda-files '("~/org/"))
         (setq org-catch-invisible-edits (quote show-and-error))
         (setq org-default-notes-file (concat org-directory "/capture.org"))
         (setq org-capture-templates
               '(;; other entries
                 ("t" "todo" entry
                  (file "~/org/capture.org")
                  "* TODO %?\n  %i\n  %a")
                 ("c" "coronavirus" entry (file+datetree 
                                           "~/org/20200314210447_coronavirus.org")
                  "* %^{Heading}")
                 ;; other entries
                 ))
         ;;open links in same window
         (delete '(file . find-file-other-window) org-link-frame-setup)
         (add-to-list 'org-link-frame-setup '(file . find-file))
         (global-set-key (kbd "C-c l") 'org-store-link)
         (global-set-key (kbd "C-c a") 'org-agenda)
         (global-set-key (kbd "C-c c") 'org-capture))
     #+END_SRC 
**** TODO change the keybindings for create link/open link
*** org-roam
    :PROPERTIES:
    :ID:       D2D0F738-E9C0-4A84-B1B5-660BC7B8DB3E
    :END:
    #+BEGIN_SRC emacs-lisp
      (use-package org-roam
        :after org
        :hook 
        (after-init . org-roam-mode)
        :straight (:host github :repo "jethrokuan/org-roam" :branch "develop")
        :config
        (setq org-roam-directory "~/org/"))
    #+END_SRC 
*** org-journal
    #+BEGIN_SRC emacs-lisp
      (use-package org-journal
        :custom
        (org-journal-find-file 'find-file)
        (org-journal-dir "~/org/journal/")
        (org-journal-date-format "%A, %d %B %Y"))

    #+END_SRC 
*** org-bullets
    #+BEGIN_SRC emacs-lisp
      (use-package org-bullets
        :hook (org-mode . (lambda () (org-bullets-mode t))))
    #+END_SRC 
** Evil
*** evil
    #+BEGIN_SRC emacs-lisp
      (use-package evil
        :init
        (setq evil-want-integration t) ;; This is optional since it's already set to t by default.
        (setq evil-want-keybinding nil)
        :config 
        ;; Make evil-mode up/down operate in screen lines instead of logical lines
        (evil-mode t)
        (define-key evil-motion-state-map "j" 'evil-next-visual-line)
        (define-key evil-motion-state-map "k" 'evil-previous-visual-line)
        ;; Also in visual mode
        (define-key evil-visual-state-map "j" 'evil-next-visual-line)
        (define-key evil-visual-state-map "k" 'evil-previous-visual-line))

    #+END_SRC 
*** evil-collection
    #+BEGIN_SRC emacs-lisp
      (use-package evil-collection
        :after evil
        :config
        (evil-collection-init))

    #+END_SRC 
*** evil-org
    #+BEGIN_SRC emacs-lisp
      (use-package evil-org
        :after org
        :config
        (add-hook 'org-mode-hook 'evil-org-mode)
        (add-hook 'evil-org-mode-hook
                  (lambda ()
                    (evil-org-set-key-theme '(textobjects insert navigation additional shift todo heading))))
        (define-key evil-normal-state-map (kbd "0") 'evil-beginning-of-line)
        (define-key evil-normal-state-map (kbd "$") 'evil-end-of-line)
        (require 'evil-org-agenda)
        (evil-org-agenda-set-keys))
      ;; (setq evil-want-C-i-jump nil) ;; C-i and TAB are same in terminal

    #+END_SRC 
*** evil-magit
    #+BEGIN_SRC emacs-lisp
      (use-package evil-magit
        :after evil
        :config
        (evil-magit-init))
    #+END_SRC
*** evil-visualstar

    #+BEGIN_SRC emacs-lisp
      (use-package evil-visualstar
        :config
        (global-evil-visualstar-mode))
    #+END_SRC 
*** evil-terminal-cursor-changer
    #+BEGIN_SRC emacs-lisp
      (unless (display-graphic-p)
        (use-package evil-terminal-cursor-changer
          :after evil
          :init
          (setq evil-motion-state-cursor 'box)  ; █
          (setq evil-visual-state-cursor 'box)  ; █
          (setq evil-normal-state-cursor 'box)  ; █
          (setq evil-insert-state-cursor 'bar)  ; ⎸
          (setq evil-emacs-state-cursor  'hbar) ; _
          :config
          (etcc-on)))

    #+END_SRC 
*** evil-commentary
    #+BEGIN_SRC emacs-lisp
      (use-package evil-commentary
        :after evil
        :config 
        (evil-commentary-mode t))

    #+END_SRC 
*** evil-leader
    Syntactic sugar for creating vim-like leader keybindings.
    #+BEGIN_SRC emacs-lisp
      (defun find-todo-file ()
        "Edit the todo.org file, in *this* window."
        (interactive)
        (find-file (concat org-directory "/todo.org")))

      (use-package evil-leader
        :after evil
        :config
        (evil-leader/set-leader "<SPC>")
        (evil-leader/set-key ;active in all modes
          "<SPC>" 'helm-M-x
          "a" 'org-agenda
          "b" 'switch-to-buffer
          "f" 'helm-find-files
          "g" 'magit-status
          "h i" 'info
          "h k" 'describe-key
          "h m" 'describe-mode
          "h o" 'describe-symbol
          "h v" 'describe-variable
          "h w" 'where-is
          "i" 'er-find-user-init-file
          "j" 'org-journal-new-entry
          "k" 'kill-this-buffer
          "K" 'kill-buffer-and-window
          "n" 'switch-to-next-buffer
          ;; "o" 'xah-new-empty-buffer
          "o f" 'open-dir-in-finder
          "o i" 'open-dir-in-iterm
          "p" 'switch-to-prev-buffer
          "q" 'delete-other-windows
          "s h" 'evil-window-left
          "s j" 'evil-window-down
          "s k" 'evil-window-up
          "s l" 'evil-window-right
          "t l" 'load-theme
          "t s" 'switch-theme
          "t d" 'disable-theme
          "w" 'save-buffer;)
        ;; (evil-leader/set-key-for-mode 'org-mode ;just for org-mode, normal state
          "'" 'org-edit-special
          "r c" 'org-capture
          "r f" 'org-roam-find-file
          "r g" 'org-roam-show-graph
          "r i" 'org-roam-insert
          "r l" 'org-roam
          "r o" 'org-open-at-point)
        (evil-leader/set-key-for-mode 'LaTeX-mode
          "c a" 'LaTeX-command-run-all 
          "c c" 'LaTeX-command-master
          "c e" 'LaTeX-environment)
        (global-evil-leader-mode t))
    #+END_SRC 
*** evil-surround
    #+BEGIN_SRC emacs-lisp
      (use-package evil-surround
        :after evil
        :config
        (global-evil-surround-mode t))
    #+END_SRC 
** helm
   #+BEGIN_SRC emacs-lisp
     (use-package helm
       :init
       (setq helm-completion-style 'emacs)
       (setq completion-styles '(helm-flex))
       :config 
  
       (define-key helm-map (kbd "C-w") 'evil-delete-backward-word)
       (global-set-key (kbd "M-x") 'helm-M-x)
       (helm-mode t))

   #+END_SRC 
** company
   #+BEGIN_SRC emacs-lisp
          (use-package company
            :config
            (define-key company-active-map (kbd "C-w") 'evil-delete-backward-word)
            (global-company-mode t))

   #+END_SRC 
** TRAMP
   #+BEGIN_SRC emacs-lisp

     ;; TRAMP: disable version control to avoid delays:
     (setq vc-ignore-dir-regexp
           (format "\\(%s\\)\\|\\(%s\\)"
                   vc-ignore-dir-regexp
                   tramp-file-name-regexp))
   #+END_SRC 
** LaTeX
   See [[file:custom.el][custom.el]] for variables edited via customization buffer.
   #+BEGIN_SRC emacs-lisp
     (use-package auctex
       :defer t
       :config
       (setq TeX-auto-save t)
       (setcdr (assoc "LaTeX" TeX-command-list)
               '("%`%l%(mode) -shell-escape%' %t"
                 TeX-run-TeX nil (latex-mode doctex-mode) :help "Run LaTeX")))

   #+END_SRC 
** magit
   #+BEGIN_SRC emacs-lisp
     (use-package magit)

   #+END_SRC 
** mac-pseudo-daemon
   #+BEGIN_SRC emacs-lisp
     (use-package mac-pseudo-daemon
       :straight (mac-pseudo-daemon :type git :host github :repo "DarwinAwardWinner/mac-pseudo-daemon")
       :config
       (mac-pseudo-daemon-mode t))
   #+END_SRC 
